Programm        ::= Statement Programm | Statement ;
Statement       ::= FuncDeclaration | VarDeclaration | FuncCall ";"  | Assignment | IfStatement | WhileStatement |
                    In ";" | Out | ";";

VarDeclaration  ::= Declaration Identifier [ "=" Expression | In ] ";"

Assignment      ::= Identifier "=" (Expression | In) ";" ;

IfStatement     ::= If "(" Expression ")" "{" { Statement } "}" ;

WhileStatement  ::= While "(" Expression ")" "{" { Statement | Break | Continue } "}" ;

FuncDeclaration ::= Declaration Identifier "(" Parameters ")" "{" { Statement | Return } "}" ;
Parameters      ::= [Declaration Identifier {"," Parameters } ] ;

FuncCall        ::= Identifier "(" [Arguments] ")" ;
Arguments       ::= [ Identifier { "," Arguments } ] ;

If              ::= "if" ;
While           ::= "while" ;
Declaration     ::= "int" ;
Break           ::= "break" ";" ;
Continue        ::= "continue" ";" ;
In              ::= "input()" ;
Out             ::= "print" "(" Expression ")" ";" ;
Return          ::= "return" Expression ";" ;

Expression      ::= Priority5

Priority5       ::= Priority4 [ ( "and" | "or" ) Priority5 ] ;
Priority4       ::= Priority3 [ ( "==" | "!=" | ">=" | "<=" | ">" | "<") Priority4] ;
Priority3       ::= Priority2 [ { "+" | "-" } Priority3 ] ;
Priority2       ::= Priority1 [ ( "*" | "/" ) Priority2 ] ;
Priority1       ::= Priority0 [ "^" Priority1] ;
Priority0       ::= "sin" "(" Priority5 ")" | "cos" "(" Priority5 ")" | "sqrt" "(" Priority5 ")" | "!" Value | Value ;
Value           ::= "(" Priority5 ")" | "(" "-" Number ")" | In | Number | FuncCall | Identifier ;

Identifier      ::= Character { Character | Digit } ;
Number          ::= Digit { Digit } ;

Character       ::= "A" | "B" | "C" | "D" | "E" | "F" | "G" |
                    "H" | "I" | "J" | "K" | "L" | "M" | "N" |
                    "O" | "P" | "Q" | "R" | "S" | "T" | "U" |
                    "V" | "W" | "X" | "Y" | "Z" | "a" | "b" |
                    "c" | "d" | "e" | "f" | "g" | "h" | "i" |
                    "j" | "k" | "l" | "m" | "n" | "o" | "p" |
                    "q" | "r" | "s" | "t" | "u" | "v" | "w" |
                    "x" | "y" | "z" | "_"
Digit           ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;
